%import common.INT -> INT
%import common.CNAME -> ID
%import common.WS
%ignore WS
_TRUE.2: "true"
_FALSE.2: "false"
_STRING: /"[^"]*"/
?expr: expr "+" term -> plus
     | expr "-" term -> minus
     | expr "||" term -> or
     | term
?term: term "*" factor -> times
     | term "/" factor -> divide
     | term "&&" factor -> and
     | factor
?factor: "-" factor -> neg
     | "!" factor -> not
     | atom
?atom: ID -> id
     | INT -> int
     | _TRUE -> true
     | _FALSE -> false
     | _STRING -> string
     | "(" expr ")"
     | "let" ID "=" expr "in" expr "end" -> let
     | "if" expr "then" expr "else" expr -> if